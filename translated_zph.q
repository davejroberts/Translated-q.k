.z.ph:{x:.h.uh $[type x; x; first x]; 
    tf: $[2 = system["e"]; {.Q.trp[y; z; {.h.he y, "\n", .Q.sbt @ neg[x]_z}[x;]]}; {x; @[y; z; .h.he]}];
    $[not count x;
            .h.hy[`htm] .h.fram[string .z.f; x] ("?"; "?", first x idesc x: string system["v"], system["b"]);
        x ~ enlist "?";
            .h.hp @ {.h.hb["?",x; x]}'[n iasc n: string system["v"], system["b"]];
        "?[" ~ 2# x;     
            @[{.h.hp .h.jx["J"$ 2_ x;.h.R]}; x; .h.he]; 
        "?" = first x;
            tf[5; {.h.hp .h.jx[0j;.h.R::1_x]}; x];
        "?" in x;
            tf[4; {.h.hy[t] @ ` sv .h.tx[t: last ` vs `$ n# x] @ value (1 + n: x? "?") _ x}; x];
        count r: @[1::; `$":", p: .h.HOME, "/", x; ""];  
            .h.hy[`$ last @ "." vs x; "c"$ r];
        .h.hn["404 Not Found";`txt] p,": not found"
        ]
    }
